/*
 * generated by Xtext 2.19.0
 */
package com.zazuko.rdfmapping.dsl.formatting2

import com.google.inject.Inject
import com.zazuko.rdfmapping.dsl.rdfMapping.Datatype
import com.zazuko.rdfmapping.dsl.rdfMapping.DatatypesDefinition
import com.zazuko.rdfmapping.dsl.rdfMapping.Domainmodel
import com.zazuko.rdfmapping.dsl.rdfMapping.LogicalSource
import com.zazuko.rdfmapping.dsl.rdfMapping.Prefix
import com.zazuko.rdfmapping.dsl.rdfMapping.RdfClass
import com.zazuko.rdfmapping.dsl.rdfMapping.RdfMappingPackage
import com.zazuko.rdfmapping.dsl.rdfMapping.RdfProperty
import com.zazuko.rdfmapping.dsl.rdfMapping.Referenceable
import com.zazuko.rdfmapping.dsl.rdfMapping.SourceGroup
import com.zazuko.rdfmapping.dsl.rdfMapping.Vocabulary
import com.zazuko.rdfmapping.dsl.services.RdfMappingGrammarAccess
import org.eclipse.xtext.formatting2.AbstractFormatter2
import org.eclipse.xtext.formatting2.IFormattableDocument

class RdfMappingFormatter extends AbstractFormatter2 {

	@Inject RdfMappingGrammarAccess ga
	RdfMappingPackage pkg = RdfMappingPackage.eINSTANCE;

	def dispatch void format(Domainmodel it, extension IFormattableDocument document) {
		elements.forEach[format]; 
	}

	def dispatch void format(SourceGroup it, extension IFormattableDocument document) {
		for (LogicalSource current : logicalSources) {
			current.format.append[newLine];
		}
	}
	
	def dispatch void format(DatatypesDefinition it, extension IFormattableDocument document) {
		interior[indent];

		regionFor.keyword(ga.datatypesDefinitionAccess.datatypesKeyword_0).append[oneSpace];
		regionFor.ruleCall(ga.datatypesDefinitionAccess.BLOCK_BEGINTerminalRuleCall_2) .prepend[oneSpace];
		
		prefix?.format;
		types.forEach[format];
		
		regionFor.ruleCall(ga.datatypesDefinitionAccess.BLOCK_ENDTerminalRuleCall_5) .prepend[setNewLines(1)].append[setNewLines(2)];
	}
	
	def dispatch void format(Prefix it, extension IFormattableDocument document) {
		prepend[setNewLines(1)];
		regionFor.keyword(ga.prefixAccess.prefixKeyword_0).append[oneSpace];
		regionFor.feature(pkg.prefix_Iri).prepend[oneSpace]
	}
	
	def dispatch void format(Datatype it, extension IFormattableDocument document) {
		prepend[setNewLines(1)]
		regionFor.feature(pkg.referenceable_Value).prepend[oneSpace]
	}
	
	def dispatch void format(Vocabulary it, extension IFormattableDocument document) {
		interior(
			regionFor.ruleCall(ga.vocabularyAccess.BLOCK_BEGINTerminalRuleCall_2),
			regionFor.ruleCall(ga.vocabularyAccess.BLOCK_ENDTerminalRuleCall_6)
		)[indent];
		
		

		regionFor.keyword(ga.vocabularyAccess.vocabularyKeyword_0).append[oneSpace];
		regionFor.ruleCall(ga.vocabularyAccess.BLOCK_BEGINTerminalRuleCall_2).prepend[oneSpace];
		
		prefix?.format

		regionFor.keyword(ga.vocabularyAccess.classesKeyword_4_0).prepend[setNewLines(1)];
		
		classes.forEach[format];
		
		regionFor.keyword(ga.vocabularyAccess.propertiesKeyword_5_0).prepend[setNewLines(0, 1, 1)];
		properties.forEach[format];

		regionFor.ruleCall(ga.vocabularyAccess.BLOCK_ENDTerminalRuleCall_6) .prepend[setNewLines(1)].append[setNewLines(2)];
	}

	def dispatch void format(RdfClass it, extension IFormattableDocument document) {
		prepend[setNewLines(1)];
		regionFor.feature(pkg.rdfClass_Value).prepend[oneSpace];
	}
	
	def dispatch void format(RdfProperty it, extension IFormattableDocument document) {
		prepend[setNewLines(1)];
		regionFor.feature(pkg.rdfClass_Value).prepend[oneSpace];
	}
	
	def dispatch void format(LogicalSource it, extension IFormattableDocument document) {
		// regionFor.feature(RdfMappingPackage.eINSTANCE.getLogicalSource_TypeRef).append([newLine]);
		interior[indent];

		regionFor.keyword(ga.logicalSourceAccess.logicalSourceKeyword_0).append[oneSpace];
		regionFor.ruleCall(ga.logicalSourceAccess.BLOCK_BEGINTerminalRuleCall_2).prepend[oneSpace];
		regionFor.keyword(ga.logicalSourceAccess.typeKeyword_3_0).prepend[setNewLines(1)].append[oneSpace];
		regionFor.keyword(ga.logicalSourceAccess.sourceKeyword_4_0).prepend[setNewLines(1)].append[oneSpace];
		regionFor.keyword(ga.logicalSourceAccess.dialectKeyword_5_0).prepend[setNewLines(1)].append[oneSpace];
		regionFor.keyword(ga.logicalSourceAccess.referenceablesKeyword_6).prepend[setNewLines(2)];

		// regionFor.feature(RdfMappingPackage.eINSTANCE.logicalSource_Referenceables).prepend[indent]
		referenceables.forEach[format];

		regionFor.ruleCall(ga.logicalSourceAccess.BLOCK_ENDTerminalRuleCall_8) .prepend[setNewLines(1)].append[setNewLines(2)];
	}

	def dispatch void format(Referenceable it, extension IFormattableDocument document) {
		prepend[setNewLines(1)]
		regionFor.feature(pkg.referenceable_Name).append[oneSpace]
	}

}
