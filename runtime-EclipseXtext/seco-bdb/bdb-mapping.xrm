output r2rml

map Amt from bewilligungen.t_amt {
	subject template "https://permits.zazukoians.org/agencies/{0}" with AMT_ID;

	types
		schema.GovernmentOrganization

	properties
		schema.alternateName from AMT_ABK_DE with language-tag de;
		schema.alternateName from AMT_ABK_FR with language-tag fr;
		schema.alternateName from AMT_ABK_EN with language-tag en;
		schema.alternateName from AMT_ABK_IT with language-tag it;

		schema.name from AMT_NAME_DE with language-tag de;
		schema.name from AMT_NAME_FR with language-tag fr;
		schema.name from AMT_NAME_EN with language-tag en;
		schema.name from AMT_NAME_IT with language-tag it;
}

map Bewilligung from bewilligungen.t_bewilligung {
	subject template "https://permits.zazukoians.org/permits/{0}" with BEW_ID;

	types
		schema.GovernmentPermit

	properties
		schema.description from BEW_BESCHR_DE with language-tag de;
		schema.description from BEW_BESCHR_FR with language-tag fr;
		schema.description from BEW_BESCHR_EN with language-tag en;
		schema.description from BEW_BESCHR_IT with language-tag it;

		schema.permitAudience template "https://permits.zazukoians.org/permitaudiences/{0}" with BEW_BET_PERS_EVENT;
		bdb.permitLevel template "https://permits.zazukoians.org/permitlevels/{0}" with BEW_TYPE;
		bdb.status template "https://permits.zazukoians.org/statuses/{0}" with BEW_STATUS;

		schema.url from BEW_KONTAKT_URL_DE with language-tag de;
		schema.url from BEW_KONTAKT_URL_FR with language-tag fr;
		schema.url from BEW_KONTAKT_URL_EN with language-tag en;
		schema.url from BEW_KONTAKT_URL_IT with language-tag it;

		schema.name from BEW_TITLE_DE with language-tag de;
		schema.name from BEW_TITLE_FR with language-tag fr;
		schema.name from BEW_TITLE_EN with language-tag en;
		schema.name from BEW_TITLE_IT with language-tag it;

		bdb.legalFoundation from BEW_RECHT_GRUND_DE with language-tag de;
		bdb.legalFoundation from BEW_RECHT_GRUND_FR with language-tag fr;
		bdb.legalFoundation from BEW_RECHT_GRUND_EN with language-tag en;
		bdb.legalFoundation from BEW_RECHT_GRUND_IT with language-tag it;

		schema.dateModified from BEW_LAST_CHANGED;

		bdb.agency link Amt with BEW_AMT_LNK;
		ex.gemeinde link Gemeinde with BEW_GEMEINDE;
}

map KontaktAusBewilligung from bewilligungen.t_bewilligung {
	subject template "http://example.org/kontaktbew/{0}" with BEW_ID;

	types
		ex.KontaktAusBewilligung

	properties
		ex.bewilligung link Bewilligung with BEW_ID;

		bdb.contact from BEW_KONTAKT_DE with language-tag de;
		bdb.contact from BEW_KONTAKT_FR with language-tag fr;
		bdb.contact from BEW_KONTAKT_EN with language-tag en;
		bdb.contact from BEW_KONTAKT_IT with language-tag it;

		ex.kontaktDE from BEW_KONTAKT_DE;
		ex.kontaktFR from BEW_KONTAKT_FR;
		ex.kontaktEN from BEW_KONTAKT_EN;
		ex.kontaktIT from BEW_KONTAKT_IT;

		schema.address from BEW_KONTAKT_ADR;
		schema.faxNumber from BEW_KONTAKT_FAX;
		schema.email from BEW_KONTAKT_MAIL;
		schema.telephone from BEW_KONTAKT_TEL;
}

map KantonInfo from bewilligungen.t_kt_info {
	subject template "http://example.org/kantoninfo/{0}{1}" with KTI_KANTON KTI_ID;

	types
		ex.KantonInfo

	properties
		ex.bewilligung link Bewilligung with KTI_BEW_LNK;
		schema.url from KTI_INTERNET;
		ex.kanton from KTI_KANTON;
}

map KontaktAusKantonInfo from bewilligungen.t_kt_info {
	subject template "http://example.org/kontaktkantoninfo/{0}" with KTI_ID;

	types
		ex.KontaktKantonInfo

	properties
		ex.kantonInfo link KantonInfo with KTI_KANTON KTI_ID;
		ex.kanton from KTI_KANTON;

		bdb.contact from KTI_OFFICE;
		schema.email from KTI_EMAIL;
		schema.telephone from KTI_TELEPHONE;			
}

map Gemeinde from bewilligungen.t_gemeinde {
	subject template "http://example.org/gemeinde/{0}" with GDE_PLZ;

	types
		ex.Gemeinde

	properties
		ex.id from GDE_PLZ;
		ex.plz from GDE_PLZ_PA with datatype xsd.int;
		ex.ort from GDE_ORT_PA;
}
